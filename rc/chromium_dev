#!/usr/bin/env zsh

# Chromium / ChromiumOS development helpers
#
# Recommended quality of life things in .localrc
#
#     export BOARD=<DUT_BOARD_NAME>
#     export DUT_IP=<DUT_DIRECT_CONNECT_IP>

function _get_cr_build_desc() {
  IFS="/" read name cr_build <<< ${CR_BUILD:-"out/Debug"}
  echo $cr_build
}

# $CR_BUILD build config prompt
function crbuild() {
  cr_git_remote=$( git remote -v 2>/dev/null | grep -c chromium/src.git )
  if [ $cr_git_remote -gt 0 ]; then
    echo "%K{043}%F{232} â›­ $( _get_cr_build_desc ) %k%f"
  fi
}

wacky_theme_left_functions+=( crbuild )

# AutoNinja for Chromium
#
# Abbreviated from: AutoNinja + Lacros + eXecute + Xvfb
#
# This script reads from $CR_BUILD env variable to pick the build directory.
#
# Quick reference (need the following aliases):
#   anx chrome --extra-options : Compile and run Chrome with --extra-options
#   anlx chrome --extra-options : Compile Ash and Lacros chrome, run Ash chrome with lacros support and --extra-options
#   anxx browsertest: Compile and run browsertest in the background
#   anlxx browsertest: Compile and run lacros browsertest with default test runner in the background
#   anx --no-network ... : Same as `anx`, but pretends to disconnect from the Internet (by isolating `target` in its own network namespace)
#
# WARN: This definately won't work on bash due to `zparseopts`.
an() {
  #
  zparseopts -E -D -a opts \
    x -exec \
    X -xvfb \
    l -lacros \
    N -no-network

  local run=$(( $opts[(Ie)-x] || $opts[(Ie)--exec] ))
  local xvfb=$(( $opts[(Ie)-X] || $opts[(Ie)--xvfb] ))
  local lacros=$(( $opts[(Ie)-l] || $opts[(Ie)--lacros] ))
  local no_network=$(( $opts[(Ie)-N] || $opts[(Ie)--no-network] ))

  if [ $# -lt 1 ]; then
    echo >&2 "Invalid usage: missing <target>"
    echo >&2 ""
    echo >&2 "Usage: an [-xXl] <target> [opts]"
    echo >&2 "  Abbreviated from: AutoNinja [eXecute] [Lacros] [Xvfb]"
    return 1
  fi

  local target=$1

  local default_build=${CR_BUILD:-"out/Debug"}
  local build_dir="${default_build}"
  if [ $lacros -eq 1 ]; then
    build_dir="out/lacros"
  fi

  local target="$1"
  shift 1

  ninja -j 2048 -C $build_dir $target || return 1
  if [[ $target == "chrome" && $lacros == 1 ]]; then
    # Also build ash chrome if we are building lacros chrome.
    ninja -j 2048 -C $default_build $target || return 1
  fi

  if [ $run -eq 1 ]; then
    # Network helper
    local network_helper=" "

    if [ $no_network -eq 1 ]; then
      if ! type "sudo" > /dev/null || ! type "ip" > /dev/null ; then
        print -rP "%F{179}sudo and/or ip not found, --no-network will be ignored%f"
      fi

      # Validate sudo, which is required for `ip netns exec`.
      # Assumes a reasonable sudo authentication timeout.
      local newline=$'\n'
      local sudo_prompt=$( print -rP '%F{220}chromium network helper needs to configure network namespaces.${newline}[sudo] password for %%u: %f' )
      sudo -vp "$sudo_prompt"

      local effective_user=$( whoami )
      local test_ns=chromium_test

      # Create network namespace if necessary.
      netns_found=$( ip netns list | grep -Fxc $test_ns )
      if [ $netns_found -ne 1 ] ; then
        echo "Creating network namespace for testing: $test_ns"
        sudo ip netns add $test_ns && echo "netns $test_ns created"
      fi

      network_helper="sudo ip netns exec $test_ns sudo -u $effective_user"
    fi

    if [[ $target == *"test"* ]]; then
      # Run test. Prepend lacros wrapper is necessary.
      local maybe_lacros_wrapper=" "
      if [ $lacros -eq 1 ]; then
        # determine lacros launch helper.
        # it is later expanded with $( echo $wrapper ) to spread the args.
        maybe_lacros_wrapper="./build/lacros/test_runner.py test"
      fi

      if [ $xvfb -eq 1 ]; then
        # wrap with dummy_home and xvfb, so we don't get windows showing on
        # desktop, and keep the real homedir untouched.
        $( echo $network_helper ) ./testing/run_with_dummy_home.py ./testing/xvfb.py $( echo $maybe_lacros_wrapper ) ${build_dir}/${target} $@
      else
        # run on active display, and allow human intervention
        $( echo $network_helper ) $( echo $maybe_lacros_wrapper ) ${build_dir}/${target} $@ \
            --enable-pixel-output-in-tests \
            --ui-test-action-max-timeout=1000000 \
            --test-launcher-timeout=1000000
      fi
    else
      # Prepend useful args.
      local clever_args="--use-system-clipboard "
      if [[ $target == "chrome" ]]; then
        # Clever screen resolution.
        if [[ $DISPLAY == ":20" ]]; then
          # For CRD, guess remote display's resolution.
          local screen_wh=$( xrandr --current 2>&1 | grep -oP '(?<=current )\d+ x \d+' )
          if [[ "$screen_wh" == "" ]] ; then        # Unable to detect display
            echo "Can't detect current display, abort." >&2
            return 1
          fi

          # Reset CRD resolution if it looks like MBP HiDPI.
          local screen_w=$( echo $screen_wh | grep -oP -m1 '\d+' | head -n1 )
          local screen_h=$( echo $screen_wh | grep -oP '(<= x )\d+')

          if   [[ "$screen_w" -lt 1681 ]] ; then
            # MacOS 15" HiDPI scaled, as we don't have smaller screens
            # Reset to MBP'a native resolution. CRD will keep using it until window resize.
            # 2880x1720 has to be defined in CRD default resolutions.
            xrandr -s 2880x1720
            screen_w=2880
            screen_h=1720
          fi

          if   [[ "$screen_w" -lt 2561 ]] ; then
            echo "Likely scaled 4K HiDPI." >&2
            # 4K monitor macOS scaled HiDPI.
            # Also works for corp Z27 monitor, effectively renders at 4K then scale down.
            # 3840x2000 has to be defined.
            xrandr -s 3840x2000
            screen_w=3840
            screen_h=2000
          fi

          if   [[ "$screen_w" == "2880" ]] ; then   # Corp Macbook Pro 15" 2018
            clever_args="$clever_args --force-device-scale-factor=2 --ash-host-window-bounds=2880x1700\*2 "
          elif [[ "$screen_w" == "3840" ]] ; then   # Home 4K monitor
            clever_args="$clever_args --force-device-scale-factor=2 --ash-host-window-bounds=3840x2000\*2 "
          else                                 # Unconfigured monitor
            echo "Current display resolution isn't configured (horizontal = ${screen_w} px), using defaults." >&2
            clever_args="$clever_args --force-device-scale-factor=1 --ash-host-window-bounds=1280x720\*1 "
          fi
        else
          # Local session. Assuming corp 27" dual-monitor setup.
          clever_args="$clever_args --force-device-scale-factor=1.3 --ash-host-window-bounds=2560x1360\*1.3 "
        fi
      fi
      print -rP "clever args: %F{220}$clever_args%f" >&2

      if [[ $lacros == 1 ]]; then
        # Run lacros non-test target.
        if [[ $target != "chrome" ]]; then
          echo "warning: non-chrome, non-test lacros target isn't supported."
        fi
        local xdg_dir="../ash_chrome_xdg_runtime"
        local ash_profile="../profiles/ash"
        mkdir -p ${xdg_dir}
        XGD_RUNTIME_DIR=${xdg_dir} $( echo $network_helper ) ${default_build}/chrome \
            --user-data-dir=$ash_profile \
            --enable-wayland-server \
            --no-startup-window \
            --login-manager --login-profile=user \
            --enable-features=LacrosSupport,LacrosPrimary,LacrosOnly \
            --lacros-chrome-path=$build_dir \
            $@
      else
        # Run non-lacros target.
        $( echo $network_helper ) ${build_dir}/${target} $( echo $clever_args ) $@
      fi
    fi
  fi
}

alias anx="an -x"
alias anxx="an -x -X"
alias anl="an -l"
alias anlx="an -l -x"
alias anlxx="an -l -x -X"


# Compile and Deploy ChRome to Chrome OS device (shell-less flow).
#  - `cros_board` gclient variable must be set.
#  - BOARD env variable must be set.
# For code-server setup, prepend BOARD=<board> to the command line.
dcr() {
  if [[ -z "${BOARD}" ]] ; then
    echo >&2 "BOARD env variable is not set."
    return 1
  fi

  if [ $# -lt 1 ]; then
    echo >&2 "Usage: dcr <device> [opts]"
    echo >&2 "  Abbreviated from: Deploy ChRome"
    return 1
  fi

  local device="$1"
  shift 1

  # TODO: Add a lacros (or ash+lacros) variant 

  cros chrome-sdk --no-shell --log-level=info --nostart-goma --chrome-branding --board=${BOARD}
  grep -qF 'use_goma' out_${BOARD}/Release/args.gn || echo 'use_goma = true' >> out_${BOARD}/Release/args.gn
  grep -qF 'dcheck_always_on' out_${BOARD}/Release/args.gn || echo 'dcheck_always_on = true' >> out_${BOARD}/Release/args.gn
  gn gen -C out_${BOARD}/Release
  ninja -j 2048 -C out_$BOARD/Release chrome || return 1
  ./third_party/chromite/bin/deploy_chrome --build-dir=out_$BOARD/Release --device=$device --board=$BOARD $@
}

# Workaround for https://crbug.com/1345490
alias gn="gn --script-executable=vpython3"

# Compile command database generation.
# Assuming:
#   compdb_* is called from chromium/src
#   using qjw's layout:
#     - out/Debug for ash-on-linux
#     - out/lacros for lacros-on-linux
#
# Remember to restart clangd language server (until google's clangd supports hot-reload).
# Shortcut Ctrl/Cmd+Shift+P, "restart language server"
alias compdb_ash="tools/clang/scripts/generate_compdb.py -p out/Debug > compile_commands.json"
alias compdb_lacros="tools/clang/scripts/generate_compdb.py -p out/lacros > compile_commands.json"
alias compa="compdb_ash"
alias compl="compdb_lacros"

# Print a nice warning message recommend rebooting the system to avoid
# Chrome profile being messed up by CRD.
#
# Workaround for: http://b/239093765
if command -v switch-graphical-session &> /dev/null ; then
  if [[ ! -v REAL_SWITCH_GRAPHICAL_SESSION_CMD ]] ; then
    REAL_SWITCH_GRAPHICAL_SESSION_CMD=$( which switch-graphical-session )
  fi

  switch-graphical-session() {
    "$REAL_SWITCH_GRAPHICAL_SESSION_CMD" $@
    EXIT_CODE=$?
    CAN_SWITCH=0

    for arg in "$@" ; do
      if [[ $arg =~ "local" || $arg =~ "crd" ]] ; then
        CAN_SWITCH=1
      fi
    done

    # Check if the command caused a switch. Print warning if yes.
    if [[ $EXIT_CODE == 0 && $CAN_SWITCH == 1 ]] ; then
      print -rP "%K{220}%F{196}Warning: Please reboot to avoid messed up profiles.%f%k"
    fi

    return $EXIT_CODE
  }
fi
